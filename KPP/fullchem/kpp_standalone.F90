program main

! The KPP Standalone for GEOS-Chem Mechanism Analysis
! 
! 
! Program Description:
! 
! This program runs the GEOS-Chem KPP Standalone for a given set of initial conditions.
! It reads an input file generated by the KPP Standalone Interface that generates model
! output of the full chemical state of grid cells in 3D GEOS-Chem, GCHP, and GEOS-CF runs.
! The full mechanism is run to replicate the chemistry of the specified grid cell.
! Obin Sturm (psturm@usc.edu), Michael S Long, Christoph Keller

! The KPP Standalone is adapted from a box model used in the following publication:
! Lin, H., Long, M. S., Sander, R., Sandu, A., Yantosca, R. M., Estrada, L. A., et al. (2023). 
!  An adaptive auto-reduction solver for speeding up integration of chemical kinetics in atmospheric chemistry models: 
!  Implementation and evaluation in the Kinetic Pre-Processor (KPP) version 3.0.0. 
!  Journal of Advances in Modeling Earth Systems, 15, e2022MS003293. https://doi.org/10.1029/2022MS003293

! Updates:
!  - 2024/05/06, Obin Sturm: Simplification of the code for the GEOS-Chem KPP Standalone,
!                            removed all autoreduce and convergence criteria testing.
!                            The more general tool just runs one operator timestep 
!                            and then prints out the results to an output file.

  USE GCKPP_GLOBAL
  USE GCKPP_JACOBIANSP
  USE GCKPP_PARAMETERS
  USE GCKPP_MONITOR
  USE GCKPP_MODEL
  USE KPP_STANDALONE_INIT, ONLY: read_input


  IMPLICIT NONE

  INTEGER                :: ICNTRL(20), IERR, I
  INTEGER                :: ISTATUS(20)
  INTEGER                :: fileTotSteps
  INTEGER                :: level
  REAL(dp)               :: OperatorTimestep
  REAL(dp)               :: RCNTRL(20)
  REAL(dp)               :: Hstart
  REAL(dp)               :: Hexit
  REAL(dp)               :: cosSZA
  REAL(dp)               :: RSTATE(20)
  REAL(dp)               :: T, TIN, TOUT, start, end
  REAL :: full_sumtime, full_avg

  INTEGER :: NRTOL, NSTEPSt ! Number of iterations in the timing averaging loop

  REAL(dp)             :: Vloc(NVAR), Cinit(NSPEC), R(NREACT)

  LOGICAL              :: OUTPUT
  LOGICAL              :: ReInit
  

  ! Vars for reading files
  character(len=256) :: inputfile
  character(len=256) :: outputfile


   ! Check if an argument was provided
   if (command_argument_count() .ge. 1) then
     ! Get the first argument
     call get_command_argument(1, inputfile)
     print*, 'Processing sample: ', trim(inputfile)
   else 
    print*, 'No sample provided. Exiting.'
    stop
   endif
   ! If a second argument is provided, use it as the output file
   if (command_argument_count() .ge. 2) then
     ! Get the second argument
     call get_command_argument(2, outputfile)
     print*, 'Output file: ', trim(outputfile)
   endif

  OUTPUT       = .false.
  REINIT       = .true.  ! Reset C every NITR,NRTOL iteration
!  REINIT       = .false. ! Let C evolve over the NRTOL loop
  NRTOL         = 0




  ! Read the input file
  call read_input(inputfile, R, Cinit, SPC_NAMES, Hstart, Hexit, cosSZA, level, fileTotSteps, OperatorTimestep)


  
  ! Run the full mechanism
  call fullmech(RTOL_VALUE=0.5e-2_dp)

  ! Write the output file
  if (command_argument_count() .ge. 2) then
    call write_output(inputfile,outputfile)
  endif

CONTAINS

  subroutine fullmech(RTOL_VALUE)
    USE GCKPP_INTEGRATOR
    USE GCKPP_RATES
    USE GCKPP_INITIALIZE
    USE GCKPP_GLOBAL

    IMPLICIT NONE

    REAL(dp)    :: RTOL_VALUE

    ! Set OPTIONS
    IERR      = 0                 ! Success or failure flag
    ISTATUS   = 0                 ! Rosenbrock output 
    RCNTRL    = 0.0_dp            ! Rosenbrock input
    RCNTRL(3) = Hstart
    ! write(*,'(a,f10.2)') " Hstart: ", Hstart
    RSTATE    = 0.0_dp            ! Rosenbrock output
    ICNTRL    = 0
    ICNTRL(1) = 1
    ICNTRL(2) = 0.000_dp
    ICNTRL(3) = 4
    ICNTRL(7) = 1
    ICNTRL(15) = -1
    
    ! Tolerances
    ATOL      = 1e-2_dp
    RTOL      = RTOL_VALUE ! default in GEOS-CF 2.0 is 0.5e-2_dp

    ! Set ENV
    T    = 0d0
    TIN  = T
    TOUT = T + OperatorTimestep
    TEMP = 298.
        
    full_avg     = 0.
    full_sumtime = 0.
    start        = 0.
    end          = 0.

    ! Initialize concentrations
    C(1:NSPEC) = Cinit(1:NSPEC)
    ! Assign RCONST to rate constants from file
    ! rather than
    !  call Update_RCONST()
    RCONST = R

    ! Integrate the mechanism for an operator timestep
    CALL Integrate( TIN,    TOUT,    ICNTRL,      &
         RCNTRL, ISTATUS, RSTATE, IERR )
    NSTEPSt = ISTATUS(3)
    write(*,'(a,i5)') " Number of internal timesteps (from 3D run): ", fileTotSteps
    write(*,'(a,i5)') " Number of internal timesteps ( standalone): ", ISTATUS(3)
    ! write Hexit for 3D vs standalone
    write(*,'(a,f10.2)') " Hexit (from 3D run): ", Hexit
    write(*,'(a,f10.2)') " Hexit ( standalone): ", RSTATE(2)

    ! Check if 3D results are consistent with standalone
    if (fileTotSteps /= ISTATUS(3)) then
       write(0,*) "Warning: Number of internal steps do not match 3D grid cell"
    endif
    if (abs(Hexit-RSTATE(2))/Hexit>.001) then
       write(0,*) "Warning: final timestep does not match 3D grid cell within 0.1%"
    endif

    ! Run the RTOL variation loop
    DO I=1,NRTOL       
       call Initialize()
       C(1:NSPEC) = Cinit(1:NSPEC)

       VAR(1:NVAR) => C(1:NVAR)
       FIX(1:NFIX) => C(NVAR+1:NSPEC)
       ! Set RCONST
      !  call Update_RCONST()

       CALL Fun( C, FIX, RCONST, Vloc )

       ! Get a random RTOL
       CALL RANDOM_NUMBER(RTOL)
       RTOL = 10**(-2.*RTOL)

       ! Integrate
       CALL Integrate( TIN,    TOUT,    ICNTRL,      &
            RCNTRL, ISTATUS, RSTATE, IERR )
       call cpu_time(end)
       write(*,*) "Number of internal timesteps random RTOL: ", ISTATUS(3)
    ENDDO


    return
 end subroutine fullmech

 subroutine write_output(inputfile, outputfile)
    ! USE GCKPP_GLOBAL
    character(len=256) :: outputfile
    character(len=256) :: inputfile
    character(len=256) :: header(30)
    integer :: i
    character(len=256) :: line

    ! Write meteo data lines of the input to the output file
    open(20, file=outputfile)
    open(10, file=inputfile, status='old')
    
    ! Write the header lines to the output file
    write(20, '(A)') "30"
    write(20, '(A)') "==========================================================================="
    write(20, '(A)') ""
    write(20, '(A)') "KPP Standalone Output"
    write(20, '(A)') "This file contains the concentrations of all the chemical species"
    write(20, '(A)') "in a single grid cell of a GEOS-Chem 3D run as replicated by the "
    write(20, '(A)') "KPP Standalone. Concentrations before and after the operator timestep"
    write(20, '(A)') "are in CSV format, below."
    write(20, '(A)') ""
    write(20, '(A)') "Generated by the GEOS-Chem KPP Standalone:"
    write(20, '(A)') "https://github.com/KineticPreProcessor/KPP-Standalone"
    write(20, '(A)') ""
    write(20, '(A)') "Input file used: " // trim(inputfile)

    ! Skip the first 26 lines of the input file
    do i=1,26
       read(10,'(A)') line
    enddo
    do i=27,43
       read(10,'(A)') line
       write(20,'(A)') line
    enddo
    close(10)
    write(20, '(A)') ""
    write(20, '(A)') "==========================================================================="
    write(20, '(A)') "Species Name,Initial Concentration (molec/cc),Final Concentration (molec/cc)"
    ! write the species names, initial and final concentrations
    do i=1,NSPEC
       write(20, '(A,E25.16,A,E25.16)') trim(SPC_NAMES(i))//",", Cinit(i), ",", C(i)
    enddo
    close(20)

 end subroutine write_output

end program main

